plugins {
    id 'java-library'
    id 'maven-publish'
}

apply from: '../repositories.gradle'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withSourcesJar()
}


dependencies {
    api 'org.gwtproject:gwt-user:2.12.1',
        'org.treblereel.gwt.gwtproject.resources:gwt-resources-api:202408061',
        // old version, FIXME
        'com.google.gwt:gwt-dev:2.9.0',
        'com.google.gwt:gwt-user:2.9.0'
    annotationProcessor 'org.treblereel.gwt.gwtproject.resources:gwt-resources-processor:202402231'
}

compileJava.options.sourcepath = files(processResources.destinationDir).builtBy(processResources)

tasks.register("prepareJs", Copy) {
    dependsOn project.getParent().createGiacWasmJs
    from "${project.getParent().buildDir}/binaries/giacggb.wasm"
    into file("src/main/resources/fr/grenoble/ujf/giac")
    include "giac.wasm.js"
}

tasks.sourcesJar {
    dependsOn('classes')
    from files(file("build/generated/sources/annotationProcessor/java/main/"))
}

compileJava.dependsOn prepareJs

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'giac-gwt'
            groupId = 'fr.ujf-grenoble'
            version = project.getParent().ggrev
            from components.java
            pom {
                name = 'Giac for GWT'
                description = 'GWT bining for giac.wasm.js'
            }
        }
    }
}
